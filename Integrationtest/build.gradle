dependencies {
  testCompile ext.testDependencies
  testCompile ext.integrationTestDependencies
  testCompile project(path:':Engine_Character', configuration:'tests')
  //For testing, all projects need to be compiled against.
  project.parent.subprojects.each {
    if (isRelevant(it)) {
      testCompile project(":${it.name}")
    }
  }
}

private boolean isRelevant(project) {
  return project.name != "Anathema" && project.name != "Integrationtest"
}


configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--glue', 'net.sf.anathema', 'src/test/resources']
        }
    }
}
